plugins {
	id 'fabric-loom' version '1.7-SNAPSHOT'
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

// github packages is silly
ext.constellationCreds = new URI("https://raw.githubusercontent.com/constellation-mc/artifacts/main/credentials").toURL().readLines()

repositories {
	maven {
		url = "https://api.modrinth.com/maven"
	}
	maven {
		url = "https://maven.pkg.github.com/constellation-mc/dark-matter"
		credentials {
			username = constellationCreds.get(0)
			password = "ghp_" + constellationCreds.get(1)
		}
		content {
			includeGroup "me.melontini"
		}
	}
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"ashyboxy-cart-engine" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}

}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// fabric loader 0.15.11 ships mixinextras 0.3.5, @share namespaces were added in 0.4.0
	include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:0.4.1")))

//	modImplementation "maven.modrinth:thecopperrail:0.9.1"
//	modCompileOnly "maven.modrinth:linkart:5.4.4-1.20.2-build.133"
	modRuntimeOnly "maven.modrinth:linkart:5.4.4-1.20.2-build.133"
	// using a newer version doesn't fix the issue, so use this version since it's what linkart embeds in its release
//	modRuntimeOnly "me.melontini:dark-matter:4.0.0-1.20.1-build.481"
//	modRuntimeOnly "me.melontini:dark-matter:4.1.0-1.20.1-build.86"
	// linkart only uses the base dark matter module
	modRuntimeOnly "me.melontini:dark-matter-base:4.0.0-1.20.1-build.481"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
